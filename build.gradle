plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.20"
    id "org.jetbrains.kotlin.kapt" version "1.3.20"
    id "com.github.johnrengelman.shadow" version "4.0.3"
    id "com.qixalite.spongestart2" version "4.0.0"
}

group "de.randombyte"
version "1.2.0"

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "https://jitpack.io" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.20"
    kapt "org.spongepowered:spongeapi:7.1.0"
    shadow("com.github.randombyte-developer.kosp:kosp:v2.2.3") { transitive = false }
    shadow "org.bstats:bstats-sponge:1.4"
    compile "com.github.NucleusPowered:Nucleus:1.9.0"
}

spongestart {
    minecraft "1.12.2"
}

jar.enabled = false

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "kotlin", "de.randombyte.taxation.shaded.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.taxation.shaded.kosp"

    classifier = null // Remove '-all' suffix from output file name
}
build.dependsOn shadowJar

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}